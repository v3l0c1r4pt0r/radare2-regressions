NAME=typelinks
FILE=-
CMDS=<<EOF
wx 41574156415541544989f4555389fd4881eca8030000488b3e64488b042528000000488984249803000031c0e82fb30000488d35d54e0100bf0600000 
e asm.comments=false
s 2
tl short
s +6
tl int
s 0
pd 8
EOF
EXPECT=<<EOF
            0x00000000      4157           push r15
(short)
0x00000002 = 0x5641
            0x00000006      4154           push r12
(int)
0x00000008 = 1442089289
            0x0000000e      fd             std
            0x0000000f      4881eca80300.  sub rsp, 0x3a8
            0x00000016      488b3e         mov rdi, qword [rsi]
            0x00000019      64488b042528.  mov rax, qword fs:[0x28]
EOF
RUN

NAME=typelinks 2
FILE=-
CMDS=<<EOF
wx 48656c6c341248656c6c6f00000000000000000048656c6c6f0000000000000000009090
tk WORD=type
tk type.WORD=w
tk type.WORD.size=16
tk VbHeader=struct
tk struct.VbHeader=szVbMagic,wRuntimeBuild,szLangDll,szSecLangDll
tk struct.VbHeader.szVbMagic=char,0,4
tk struct.VbHeader.wRuntimeBuild=WORD,4,0
tk struct.VbHeader.szLangDll=char,6,14
tk struct.VbHeader.szSecLangDll=char,20,14
tl VbHeader @ 0x00
pd 3
EOF
EXPECT=<<EOF
(VbHeader)
     szVbMagic : 0x00000000 = "Hell"
 wRuntimeBuild : 0x00000004 = 0x1234
     szLangDll : 0x00000006 = "Hello"
  szSecLangDll : 0x00000014 = "Hello"
            0x00000022      90             nop
            0x00000023      90             nop
EOF
RUN

NAME=jni and SoP
FILE=../bins/elf/libverifyPass.so
CMDS=<<EOF
e asm.lines.bb = false
aa
s sym.Java_com_app_ndh_NDHActivity_print
to ../bins/headers/jni.h
tl JNINativeInterface = 0x464c457f
pdf~JNINativeInterface
EOF
EXPECT=<<EOF
| 0x00000f7e      d358           ldr r3, [r2, r3]                      ; JNINativeInterface.GetStringUTFChars
| 0x0000100e      d358           ldr r3, [r2, r3]                      ; JNINativeInterface.GetStringLength
| 0x00001066      d258           ldr r2, [r2, r3]                      ; JNINativeInterface.NewStringUTF
| 0x00001114      d258           ldr r2, [r2, r3]                      ; JNINativeInterface.NewStringUTF
| 0x0000112e      d258           ldr r2, [r2, r3]                      ; JNINativeInterface.NewStringUTF
EOF
RUN

NAME=t
FILE=-
CMDS=<<EOF
t
EOF
EXPECT=<<EOF
char
char *
double
float
int
int16_t
int32_t
int64_t
int8_t
long
long long
short
size_t
uid_t
uint16_t
uint32_t
uint64_t
uint8_t
unsigned char
unsigned int
unsigned short
void *
EOF
RUN

NAME=t <type>
FILE=-
CMDS=<<EOF
t int
t-*
t int
EOF
EXPECT=<<EOF
pf d
EOF
EXPECT_ERR=<<EOF
Cannot find 'int' type
EOF
RUN

NAME=t-enum
FILE=-
CMDS=<<EOF
td enum pe_machine { IMAGE_FILE_MACHINE_IA64=0x200, IMAGE_FILE_MACHINE_I386=0x14c };
t-pe_machine
t~?pe_machine'
EXPECT=<<EOF
0
EOF
RUN

NAME=t-struct
FILE=-
CMDS=<<EOF
"td struct three_elements{int x; char y; float z;}"
t-three_elements
ts~?
EOF
EXPECT=<<EOF
0
EOF
RUN

NAME=t-union
FILE=-
CMDS=<<EOF
"td union xoo{int x; int y; int z;}"
tk*~union
t-xoo
EOF
EXPECT=<<EOF
union.xoo=x,y,z
union.xoo.size=32
union.xoo.x=int32_t,0,0
union.xoo.x.meta=0
union.xoo.y=int32_t,0,0
union.xoo.y.meta=0
union.xoo.z=int32_t,0,0
union.xoo.z.meta=0
xoo=union
EOF
RUN

NAME=typedef
FILE=-
CMDS=<<EOF
"td typedef int Abracadabra"
t-Abracadabra
t~?Abracadabra
EOF
EXPECT=<<EOF
0
EOF
RUN

NAME=t-*
FILE=-
CMDS=<<EOF
td enum pe_machine { IMAGE_FILE_MACHINE_IA64=0x200, IMAGE_FILE_MACHINE_I386=0x14c };
t-*
t
EOF
EXPECT=<<EOF
EOF
RUN

NAME=teb
FILE=-
CMDS=<<EOF
td enum pe_machine { IMAGE_FILE_MACHINE_IA64=0x200, IMAGE_FILE_MACHINE_I386=0x14c };
teb pe_machine IMAGE_FILE_MACHINE_I386
EOF
EXPECT=<<EOF
0x14c
EOF
RUN

NAME=te
FILE=-
CMDS=<<EOF
td enum pe_machine { IMAGE_FILE_MACHINE_IA64=0x200, IMAGE_FILE_MACHINE_I386=0x14c };
te pe_machine 0x14c
EOF
EXPECT=<<EOF
IMAGE_FILE_MACHINE_I386
EOF
RUN

NAME=tt
FILE=-
CMDS=<<EOF
td typedef char FILE_NAME;
tt FILE_NAME
"td typedef bool FLAG;"
tt FLAG
EOF
EXPECT=<<EOF
char
bool
EOF
RUN

NAME=td struct
FILE=-
CMDS=<<EOF
"td struct test_struct{int x;int y;};"
ts~?test_struct
EOF
EXPECT=<<EOF
1
EOF
RUN

NAME=enum32
FILE=-
CMDS=<<EOF
"td enum v { t=0x123, p=0x321 };"
te~?
tk v
tk enum.v.t
tk enum.v.p
EOF
EXPECT=<<EOF
1
enum
0x123
0x321
EOF
RUN

NAME=enum64
FILE=-
CMDS=<<EOF
"td enum v { t=0x8000000000000001, p=0x8000000000000008 };"
te~?
tk enum.v.t
tk enum.v.p
EOF
EXPECT=<<EOF
1
0x8000000000000001
0x8000000000000008
EOF
RUN

NAME=to error.h
FILE=-
CMDS=<<EOF
to ../bins/other/error.h
t
ts
t addr
EOF
EXPECT_ERR=<<EOF
../bins/other/error.h:2: warning: #error your compiler doesn't have support to my API
EOF
EXPECT=<<EOF
char
char *
double
float
int
int16_t
int32_t
int64_t
int8_t
long
long long
short
size_t
uid_t
uint16_t
uint32_t
uint64_t
uint8_t
unsigned char
unsigned int
unsigned short
void *
addr
addr_t
date
date_t
dox
dox_t
name
name_t
pf [127]z[40]zd street city zip
EOF
RUN

NAME=to test.h
FILE=-
CMDS=<<EOF
to ../bins/other/test.h
t
ts
t addr
EOF
EXPECT=<<EOF
char
char *
double
float
int
int16_t
int32_t
int64_t
int8_t
long
long long
short
size_t
uid_t
uint16_t
uint32_t
uint64_t
uint8_t
unsigned char
unsigned int
unsigned short
void *
addr
addr_t
date
date_t
dox
dox_t
name
name_t
pf [127]z[40]zd street city zip
EOF
RUN

NAME=t*
FILE=-
CMDS=<<EOF
t-*
tk a=b
t*
EOF
EXPECT=<<EOF
tk a=b
EOF
RUN

NAME=tp
FILE=-
CMDS=<<EOF
"td struct person { int age; char name[10]; }"
s 4
w Carlos
s 0
wx 13
tp person @ 0x0
EOF
EXPECT=<<EOF
  age : 0x00000000 = 19
 name : 0x00000004 = "Carlos"
EOF
RUN

NAME=unions
FILE=-
CMDS=<<EOF
"td union x{float a;int b;}"
tu
EOF
EXPECT=<<EOF
x
EOF
RUN

NAME=Types not reloaded when arch is the same
FILE=-
CMDS=<<EOF
e asm.archx86
e anal.arch=x86
e anal.bits=32
t-*
tk a=b
e asm.arch=x68
e anal.arch=x86
e anal.bits=32
tk a
EOF
EXPECT=<<EOF
b
EOF
RUN

NAME=td twice
FILE=-
CMDS=<<EOF
"td int foo(int bar);"
"td int foo(int bar);"
tk~foo
EOF
EXPECT_ERR=<<EOF
EOF
EXPECT=<<EOF
foo=func
func.foo.arg.0=int32_t,bar
func.foo.args=1
func.foo.cc=cdecl
func.foo.ret=int32_t
EOF
RUN

NAME=tk - crash
FILE=-
CMDS="tk -!!!! d'B%CCCC!9!!!!_"
EXPECT=<<EOF
0x0
EOF
RUN

NAME=struct of struct with ts and pf
FILE=-
CMDS=<<EOF
"td struct bar { int a; int b; };"
"td struct foo { int x; struct bar moo; };"
ts foo
.ts foo
EOF
EXPECT=<<EOF
pf d? x (bar)moo
   x : 0x00000000 = 0
 moo : 
                struct<bar>
   a : 0x00000004 = 0
   b : 0x00000008 = 0
EOF
RUN

NAME=Array of struct with ts
FILE=-
CMDS=<<EOF
"td struct bar { int a; int b; };"
"td struct foo { int x; struct bar moo[2]; };"
ts foo
.ts foo
EOF
EXPECT=<<EOF
pf d[2]? x (bar)moo
   x : 0x00000000 = 0
 moo : 
[
                struct<bar>
   a : 0x00000004 = 0
   b : 0x00000008 = 0

                struct<bar>
   a : 0x0000000c = 0
   b : 0x00000010 = 0
]
EOF
RUN

NAME=te <enum>
FILE=-
CMDS=<<EOF
"td enum Foo {COW=1,BAR=2};"
te
te Foo
EOF
EXPECT=<<EOF
Foo
COW = 0x1
BAR = 0x2
EOF
RUN

NAME=te <enum> <value>
FILE=-
CMDS=<<EOF
"td enum Foo {COW=1,BAR=2};"
te Foo 1
EOF
EXPECT=<<EOF
COW
EOF
RUN

NAME=teb <enum> <name>
FILE=-
CMDS=<<EOF
"td enum Foo {COW=1,BAR=2};"
teb Foo COW
EOF
EXPECT=<<EOF
0x1
EOF
RUN

NAME=tp with varname
FILE=../bins/elf/struct_sample
CMDS=<<EOF
"td struct Books {char  title[50];char  author[50]; char  subject[100];};"
aa
s main
aei
aeim
aecu 0x000006d5
?e
tp Books var_e0h
EOF
EXPECT=<<EOF

   title : 0x00177f18 = "Radare2"
  author : 0x00177f4a = "pancake"
 subject : 0x00177f7c = "Reversing"
EOF
RUN

NAME=tpx test
FILE=-
CMDS=<<EOF
"td struct foo {char* a; int b;}"
tpx foo 4141414144141414141442001000000
EOF
EXPECT=<<EOF
 a : 0x00000000 = "AAAAD.....B"
 b : 0x0000000c = 16
EOF
RUN

NAME=ta and arm
FILE=-
CMDS=<<EOF
e asm.arch = arm
e asm.bits = 32
wx 082090e5040081e5
"td struct foo {int a; int b; int c;};"
ta foo.c @ 0x00000000
ta foo.b @ 0x00000004
?e
pd 2
EOF
EXPECT=<<EOF

            0x00000000      082090e5       ldr r2, [r0, foo.c]
            0x00000004      040081e5       str r0, [r1, foo.b]
EOF
RUN

NAME=ta and mips
FILE=-
CMDS=<<EOF
e asm.arch = mips
wx 08007fac040053800800538c
"td struct foo {int a; int b; int c;};"
ta foo.c @ 0x00000000
ta foo.c @ 0x00000008
ta foo.b @ 0x00000004
?e
pd 3
EOF
EXPECT=<<EOF

            0x00000000      08007fac       sw ra, foo.c(v1)
            0x00000004      04005380       lb s3, foo.b(v0)
            0x00000008      0800538c       lw s3, foo.c(v0)
EOF
RUN

NAME=struct offset for dst operand with ta
FILE=-
CMDS=<<EOF
e asm.bits = 32
"td struct foo {int a; int b ; int c;}"
wx 8b40048951048d500883780800
?e
ta foo.b @ 0x00000000
ta foo.b @ 0x00000003
ta foo.c @ 0x00000006
ta foo.c @ 0x00000009
pd 4
EOF
EXPECT=<<EOF

            0x00000000      8b4004         mov eax, dword [eax + foo.b] ; [0x4:4]=0x508d0451
            0x00000003      895104         mov dword [ecx + foo.b], edx
            0x00000006      8d5008         lea edx, [eax + foo.c]      ; 0x8
            0x00000009      83780800       cmp dword [eax + foo.c], 0  ; [0x8:4]=0x8788308
EOF
RUN

NAME=struct offset propagate via tl
FILE=-
CMDS=<<EOF
e asm.comments = false
"td struct bar {int a ; int b; int c;}"
wx 48C7C058190000488B5004488B4808483B500448BBEFBEADDE00000000488958089090C3
af
tl bar = 0x1958
?e
pdf~bar.
EOF
EXPECT=<<EOF

|           0x00000007      488b5004       mov rdx, qword [rax + bar.b]
|           0x0000000b      488b4808       mov rcx, qword [rax + bar.c]
|           0x0000000f      483b5004       cmp rdx, qword [rax + bar.b]
|           0x0000001d      48895808       mov qword [rax + bar.c], rbx
EOF
RUN

NAME=taa with single struct
FILE=-
CMDS=<<EOF
e asm.comments = false
"td struct bar {int a ; int b; int c;}"
wx 48C7C058190000488B5004488B4808483B500448BBEFBEADDE00000000488958089090C3
tl bar = 0x1958
af
taa @ fcn.00000000
?e
pdf~bar.
EOF
EXPECT=<<EOF

|           0x00000007      488b5004       mov rdx, qword [rax + bar.b]
|           0x0000000b      488b4808       mov rcx, qword [rax + bar.c]
|           0x0000000f      483b5004       cmp rdx, qword [rax + bar.b]
|           0x0000001d      48895808       mov qword [rax + bar.c], rbx
EOF
RUN

NAME=taa with multiple struct
FILE=-
CMDS=<<EOF
e asm.comments = false
e asm.flags = false
"td struct bar {int x; int y; int z;};"
"td struct foo {int a; int b; int c;};"
wx 48C7C000010000488B5008488B480448C7C000020000488B5008488B48049090C3
tl bar = 0x200
tl foo = 0x100
af
taa
pdf
EOF
EXPECT=<<EOF
/ (fcn) fcn.00000000 33
|   fcn.00000000 ();
|           0x00000000      48c7c0000100.  mov rax, 0x100
|           0x00000007      488b5008       mov rdx, qword [rax + foo.c]
|           0x0000000b      488b4804       mov rcx, qword [rax + foo.b]
|           0x0000000f      48c7c0000200.  mov rax, 0x200
|           0x00000016      488b5008       mov rdx, qword [rax + bar.z]
|           0x0000001a      488b4804       mov rcx, qword [rax + bar.y]
|           0x0000001e      90             nop
|           0x0000001f      90             nop
\           0x00000020      c3             ret
EOF
RUN

NAME=Comments and var in struct offset propagation
FILE=../bins/elf/struct_sample
CMDS=<<EOF
aa
s main
"td struct Books {char  title[50];char  author[50]; char  subject[100];};"
tl Books = 0x00177f18
?e
pdf~Books
EOF
EXPECT=<<EOF

|           ; var struct Books @ rbp-0xe0
|           0x00000684      488d8520ffff.  lea rax, [Books]
|           0x00000695      488910         mov qword [rax], rdx        ; Books.title
|           0x00000698      488d8520ffff.  lea rax, [Books]
|           0x000006ad      488908         mov qword [rax], rcx        ; Books.author
|           0x000006b0      488d8520ffff.  lea rax, [Books]
|           0x000006c5      488930         mov qword [rax], rsi        ; Books.subject
|           0x000006d5      b800000000     mov eax, 0                  ; Books.subject
EOF
RUN

NAME=tl with ahr (return value hint)
FILE=../bins/elf/struct_2
CMDS=<<EOF
aa
"td struct Test { int a; int  b; char *c;};"
s main
ahr 0x00000597 @ 0x00000592
tl Test = 0x00002020
s 0x000005a8
?e
pd 3
EOF
EXPECT=<<EOF

|           0x000005a8      c70206000000   mov dword [edx], 6          ; Test.a
|           0x000005ae      8b55f4         mov edx, dword [Test]       ; Test.a
|           0x000005b1      c74204070000.  mov dword [edx + Test.b], 7
EOF
RUN

NAME=Basic union test
FILE=../bins/elf/union_sample
CMDS=<<EOF
aa
"td union Books {char  title[50];char  author[50]; char  subject[100];};"
tu
tu Books
s main
tl Books = 0x00177f88
afv~Books
s 0x000006d2
pd 1
EOF
EXPECT=<<EOF
Books
pf [50]z[50]z[100]z title author subject
var union Books @ rbp-0x70
|           0x000006d2      488d4590       lea rax, [Books]
EOF
RUN

NAME=ts function pointer test
FILE=-
CMDS=<<EOF
ws hello @ 0xc
"td struct foo {int x; int (*fp)(int a , int b ); char *b;};"
ts foo
.ts foo
EOF
EXPECT=<<EOF
pf dpz x fp b
  x : 0x00000000 = 0
 fp : 0x00000004 = (qword)0x0000000000000000
  b : 0x0000000c = ".hello"
EOF
RUN

NAME=ts general pointers test
FILE=-
CMDS=<<EOF
"td struct normal {int a;char b;};"
"td struct pointer {char *y;struct normal *obj;};"
ts pointer
EOF
EXPECT=<<EOF
pf zp y obj
EOF
RUN

NAME=tsc test
FILE=-
CMDS=<<EOF
"td struct three_elements{int x; char y; float z;}"
tsc
EOF
EXPECT=<<EOF
struct three_elements {int32_t x; char y; float z;};
EOF
RUN

NAME=tec test
FILE=-
CMDS=<<EOF
"td enum Foo {COW=1,BAR=2};"
tec
EOF
EXPECT=<<EOF
enum Foo {COW = 1, BAR = 2};
EOF
RUN

NAME=tuc test
FILE=-
CMDS=<<EOF
"td union x{int x; int y; int z;}"
tuc
EOF
EXPECT=<<EOF
union x {int32_t x; int32_t y; int32_t z;};
EOF
RUN

NAME=ttc test
FILE=-
CMDS=<<EOF
"td typedef char FILE_NAME;"
ttc
EOF
EXPECT=<<EOF
typedef char FILE_NAME;
EOF
RUN

NAME=tc test
FILE=-
CMDS=<<EOF
"td typedef char FILE_NAME;"
"td union x{int x; int y; int z;}"
"td enum Foo {COW=1,BAR=2};"
"td struct three_elements{int x; char y; float z;}"
tc
EOF
EXPECT=<<EOF
union x {int32_t x; int32_t y; int32_t z;};
struct three_elements {int32_t x; char y; float z;};
typedef char FILE_NAME;
enum Foo {COW = 1, BAR = 2};
EOF
RUN

NAME=typedef struct
BROKEN=1
FILE=-
CMDS=<<EXPECT
to ../bins/headers/12272.h
ts 0
ts 1
?e ----
ts A
ts B
ts O  # Broken but shouldn't hang
?e ----
tk~struct
?e
ts
EXPECT=<<RUN
pf dF num num2
pf c ch
----
pf dF num num2
pf c ch
----
0=struct
1=struct
struct.0=num,num2
struct.0.num=int32_t,0,0
struct.0.num.meta=0
struct.0.num2=double,8,0
struct.0.num2.meta=9
struct.0.size=96
struct.1=ch
struct.1.ch=char,0,0
struct.1.ch.meta=2
struct.1.size=8
struct.A=num
struct.A.num=float,0,0
struct.A.num.meta=8
struct.O=ptr
struct.O.ptr=char *,0,0
struct.O.ptr.meta=4
typedef.A=struct 0
typedef.B=struct 1
typedef.O=struct O

0
1
A
B
O
RUN

NAME=typedef struct double header
FILE=-
CMDS=<<EXPECT
to ../bins/headers/hdr1.h
to ../bins/headers/hdr2.h
ts A
ts B
?e
to ../bins/headers/hdr1.h
to ../bins/headers/hdr2.h
tk~^struct
?e
ts
EXPECT=<<RUN
pf f num
pf c ch

struct.0=num
struct.0.num=float,0,0
struct.0.num.meta=8
struct.0.size=32
struct.1=ch
struct.1.ch=char,0,0
struct.1.ch.meta=2
struct.1.size=8
struct.2=num
struct.2.num=float,0,0
struct.2.num.meta=8
struct.2.size=32
struct.3=ch
struct.3.ch=char,0,0
struct.3.ch.meta=2
struct.3.size=8

0
1
2
3
A
B
RUN
